dnl
dnl File:        configure.in
dnl Revision:    $Id: configure.ac,v 1.7.4.1 2005/09/22 21:19:27 rkowen Exp $
dnl Created:     1994/06/18
dnl Author:      Leif Hedstrom<hedstrom@inf.ethz.ch>
dnl		 Martin S. Utesch <wstd@hl.siemens.de>
dnl 		 Peter W. Osel <pwo@GUUG.DE>
dnl 		 R.K. Owen <rk@owen.sj.ca.us>
dnl
dnl Process this file with autoconf to produce a configure script
dnl You need autoconf 2.52 or better!
dnl
dnl ---------------------------------------------------------------------------
AC_INIT(modules,3.2)
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE(foreign dejagnu no-installinfo)
AC_CONFIG_SRCDIR([version.c])
AM_CONFIG_HEADER(config.h)
AC_PROG_MAKE_SET
AC_PREREQ(2.52)

dnl ---------------------------------------------------------------------------
dnl Autoheader "templates" (was acconfig.h)
AH_TOP([
/*****
 ** ** Module Header ******************************************************* **
 ** 									     **
 **   Modules Revision 3.0						     **
 **   Providing a flexible user environment				     **
 ** 									     **
 **   Description:	config.h.in (generated by autoheader from	     **
 **			configure.ac)					     **
 ** 									     **
 ** ************************************************************************ **
 ****/

/** ** Copyright *********************************************************** **
 ** 									     **
 ** Copyright 1991-1994 by John L. Furlan.                      	     **
 ** see LICENSE.GPL, which must be provided, for details		     **
 ** 									     ** 
 ** ************************************************************************ **/
])dnl
AH_TEMPLATE([TCL_MINOR_VERSION],[
TCL_MINOR_VERSION: if defined, contains the version of TCL (and TCLX)])dnl
AH_TEMPLATE([TCL_MAJOR_VERSION],[
TCL_MAJOR_VERSION: if defined, contains the version of TCL (and TCLX)])dnl

dnl ---------------------------------------------------------------------------
dnl
dnl Some special substitution we need:
dnl
dnl ---------------------------------------------------------------------------
dnl Need to know if versioning is asked for before setting certain paths
dnl --enable-versioning		Use modules versioning 	[DEF]
dnl --disable-versioning	Without  - " -
dnl ---------------------------------------------------------------------------
dnl
AC_SUBST(WANTS_VERSIONING)dnl
AC_SUBST(VERSIONING)dnl
AC_SUBST(NOTVERSIONING)dnl
AH_TEMPLATE([WANTS_VERSIONING],[
WANTS_VERSIONING Does the system want to use modules versions])dnl
AC_ARG_ENABLE(versioning,
	AC_HELP_STRING([--enable-versioning], [use modules versioning [[yes]]]),
	if test "$enableval" = "yes"; then
	    WANTS_VERSIONING=1
	    AC_DEFINE(WANTS_VERSIONING,1)
	    VERSIONING=""
	    NOTVERSIONING="#"
	else
	    WANTS_VERSIONING=0
	    AC_DEFINE(WANTS_VERSIONING,0)
	    VERSIONING="#"
	    NOTVERSIONING=""
	fi, WANTS_VERSIONING=1
	    AC_DEFINE(WANTS_VERSIONING,1)
	    VERSIONING=""
	    NOTVERSIONING="#"
)dnl
dnl
dnl ---------------------------------------------------------------------------
dnl Get Modules version & date
dnl ---------------------------------------------------------------------------
dnl DATE		Module package date
dnl VERSION		Module package version
dnl
AC_SUBST(VERSION)dnl
AC_SUBST(DATE)dnl
VERSION='(unknown)'
VERSION=`sed -n -e 's/^.*version_string.*"\(.*\)";$/\1/p' ${srcdir}/version.c`
AC_MSG_RESULT([VERSION = $VERSION])
DATE='(DDMmmYYYY)'
DATE=`sed -n -e 's/^.*date_string.*"\(.*\)";$/\1/p' ${srcdir}/version.c`
AC_MSG_RESULT([DATE = $DATE])
echo "configuring for Modules $VERSION		$DATE"
dnl ---------------------------------------------------------------------------
dnl Set PREFIX and related based on WANTS_VERSIONING
dnl ---------------------------------------------------------------------------
AC_SUBST(DEFAULTPATH)
AC_SUBST(BASEPREFIX)
DEFAULTPATH=default
AH_TEMPLATE([BASEPREFIX],[ BASEPREFIX: Usually equivalent to /usr/local])dnl
AH_TEMPLATE([PREFIX],[
PREFIX: Usually equivalent to /usr/local/Modules[/version_number]])dnl
AH_TEMPLATE([MODULES_INIT_DIR],[
MODULES_INIT_DIR: Directory that contains the modules init files.])dnl

if test "$prefix" != "NONE" ; then
  if test "$WANTS_VERSIONING" -eq 1 ; then
    DEFAULTPATH=$prefix/Modules/default
    NODEFAULTPATH=$prefix/Modules/$VERSION
    BASEPREFIX=$prefix
    prefix=$BASEPREFIX/Modules/$VERSION
    export prefix
  else
    DEFAULTPATH=$prefix/Modules
    NODEFAULTPATH=$prefix/Modules
    BASEPREFIX=$prefix
    prefix=$BASEPREFIX/Modules
    export prefix
  fi
  PREFIX=$prefix
  MODULES_INIT_DIR=$prefix/init
else
  if test "$WANTS_VERSIONING" -eq 1 ; then
    DEFAULTPATH=$ac_default_prefix/Modules/default
    NODEFAULTPATH=$ac_default_prefix/Modules/$VERSION
    BASEPREFIX=$ac_default_prefix
    ac_default_prefix=$BASEPREFIX/Modules/$VERSION
    export ax_default_prefix
  else
    DEFAULTPATH=$ac_default_prefix/Modules
    NODEFAULTPATH=$ac_default_prefix/Modules
    BASEPREFIX=$ac_default_prefix
    ac_default_prefix=$BASEPREFIX/Modules
    export ac_default_prefix
  fi
  MODULES_INIT_DIR=$ac_default_prefix/init
  PREFIX=$ac_default_prefix
fi
dnl ---------------------------------------------------------------------------
dnl --enable-use-default        Use PREFIX/default/ in init scripts [DEF]
dnl --disable-use-default       Use PREFIX/VERSION/ in init scripts
dnl ---------------------------------------------------------------------------
AC_DEFINE_UNQUOTED(MODULES_INIT_DIR, "$MODULES_INIT_DIR")
AC_ARG_ENABLE(use-default,
	AC_HELP_STRING([--enable-use-default],[use DEFAULTPATH in init scripts [[yes]]]),
	if test "$enableval" = "no" -o $WANTS_VERSIONING -eq 0; then
		DEFAULTPATH=$NODEFAULTPATH
	fi)
AC_MSG_RESULT([DEFAULTPATH = $DEFAULTPATH])
AC_MSG_RESULT([BASEPREFIX = $BASEPREFIX])
AC_MSG_RESULT([PREFIX = $PREFIX])
AC_DEFINE_UNQUOTED(BASEPREFIX, "$BASEPREFIX")
AC_DEFINE_UNQUOTED(PREFIX, "$PREFIX")
export DEFAULTPATH
export NODEFAULTPATH
export BASEPREFIX
export MODULES_INIT_DIR
dnl ---------------------------------------------------------------------------
dnl Some standard autoconfig stuff
dnl Checks for programs
dnl ---------------------------------------------------------------------------
AC_PROG_CC
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_AIX
AC_ISC_POSIX
AC_PATH_PROG([UNAME],[uname],[""])
dnl ---------------------------------------------------------------------------
AC_PATH_X
AC_PATH_XTRA
if test "$no_x" != "yes"; then

	AH_TEMPLATE([HAS_X11LIBS],[
HAS_X11LIBS:
This symbol, if defined, indicates that the C program can use X11
directly.])dnl
	AC_DEFINE(HAS_X11LIBS)dnl
	AC_CHECK_LIB(X11, XMaxRequestSize)dnl
	AC_CHECK_LIB(Xmu, XmuGetHostname)dnl
fi
dnl ---------------------------------------------------------------------------
dnl Checks for header files
dnl ---------------------------------------------------------------------------

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(string.h memory.h stdlib.h unistd.h termio.h fcntl.h ctype.h \
	stdarg.h varargs.h syslog.h)
AC_CHECK_HEADERS(errno.h sys/ioctl.h sys/termios.h sys/mode.h sys/stat.h      \
	sys/param.h)
dnl ---------------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
dnl ---------------------------------------------------------------------------
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM
dnl ---------------------------------------------------------------------------
dnl Checks for libraries
dnl ---------------------------------------------------------------------------
dnl Solaris 2.x: This requires -lnsl and -lsocket. If AC_PATH_XTRA
dnl              did not search for these, search them explicitely.
dnl              MUST be done BEFORE AC_PATH_XTRA!
dnl ---------------------------------------------------------------------------
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, t_accept)

dnl ---------------------------------------------------------------------------
dnl Checks for library functions.
dnl ---------------------------------------------------------------------------
AC_CHECK_FUNCS([strdup strtok uname gethostname getdomainname \
	mktemp tmpnam tempnam \
	syslog dup2])
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([CPPSTDIN],[
CPPSTDIN:
This symbol contains the first part of the string which will invoke
the C preprocessor on the standard input and produce to standard
output.	 Typical value of "cc -E" or "/lib/cpp".])dnl
EM_PATH_PROG_SEARCH(_CPPSTDIN, cpp, /lib /usr/lib /usr/lang \
	/usr/local/lang /usr/ccs/lib, [${CC} -E])
AC_DEFINE_UNQUOTED(CPPSTDIN, "$_CPPSTDIN")dnl
dnl
AH_TEMPLATE([CPPMINUS],[
CPPMINUS:
This symbol contains the second part of the string which will invoke
the C preprocessor on the standard input and produce to standard
output.  This symbol will have the value "-" if CPPSTDIN needs a minus
to specify standard input, otherwise the value is "".])dnl
AC_DEFINE(CPPMINUS, "")dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([PHOSTNAME],[
PHOSTNAME:
Last resort to get the hostname, if uname() and gethostname()
are not available.])dnl
AC_PATH_PROG([_PHOST],[hostname],[""])dnl
test "$_PHOST" != ""  && AC_DEFINE_UNQUOTED(PHOSTNAME, "$_PHOST")
AC_PATH_PROG(RUNTEST, runtest, "./not_installed")dnl
test "$RUNTEST" = "" && echo "Install DEJAGNU and rerun configure if you \
want to run the testsuite"

dnl
dnl Here we define some --with & --enable things:
dnl
dnl --with-force-path=<path>	With force path=<path>
dnl --without-force-path	Without  - " -		[DEF]
dnl
dnl --with-split-size[=<size>]	With split size=<size>
dnl --without-split-size	Without  - " -		[DEF]
dnl
dnl --enable-cache		With directory cache	[DEF]
dnl --disable-cache		Without  - " -
dnl
dnl --enable-shell-funcs	With sh functions	[DEF]
dnl --disable-shell-funcs	Without  - " -
dnl
dnl --enable-shell-eval		With shell alias eval
dnl --disable-shell-eval	Without  - " -		[DEF]
dnl
dnl --enable-free		With free() calls
dnl --disable-free		Without  - " -		[DEF]
dnl
dnl --with-debug[=<option>]	Set up debugging level
dnl --without-debug		Without  - " -		[DEF]
dnl
dnl --enable-version-magic      Set .version magic requirement [DEF]
dnl --disable-version-magic     .version need not have magic cookie
dnl
dnl --enable-beginenv		Use .modulesbeginenv for initial env.
dnl --disable-beginenv		Do not use .modulesbeginenv [DEF]
dnl
dnl ---------------------------------------------------------------------------
AH_VERBATIM([FORCE_PATH],[
/* FORCE_PATH:
 *     This symbol, if defined, is the "default" path built-in to the
 *     modulecmd app. Note: There is no longer a FORCE_PATH_BASE!
#undef FORCE_PATH
#define FORCE_PATH_SACRED  NULL
#define FORCE_LD_LIBRARY_PATH_SACRED NULL
])dnl
AC_ARG_WITH(force-path,
	AC_HELP_STRING([--with-force-path=<path>],[use force path=<path> [[no]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="/usr/bin"
		AC_DEFINE_UNQUOTED(FORCE_PATH, "$withval")
	fi)dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([LMSPLIT_SIZE],[
LMSPLIT_SIZE:
This symbol, if defined, will force the environment variable
_LM_FILES to be split into smaller "chunks".])dnl
AC_ARG_WITH(split-size,
	AC_HELP_STRING([--without-split-size[=<size>]],[use split size=<size> [[no]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval=900
		AC_DEFINE_UNQUOTED(LMSPLIT_SIZE, ${withval})
	fi)dnl
dnl ---------------------------------------------------------------------------
dnl --with-static[=<option>]	With static linking
dnl --without-static		Without  - " -		[DEF]
dnl STATIC		Link the binary 'static'
dnl
AC_SUBST(STATIC)dnl
dnl
AC_ARG_WITH(static,
	AC_HELP_STRING([--with-static[[=<option>]]],[use static linking [[no]]]),
	EM_SET_STATIC(STATIC, $withval))

dnl ---------------------------------------------------------------------------
AH_TEMPLATE([HAS_BOURNE_FUNCS],[
HAS_BOURNE_FUNCS:
This symbol, if defined, indicates that the Bourne shell can define aliases
as shell functions.])dnl
AC_ARG_ENABLE(shell-funcs,
	AC_HELP_STRING([--enable-shell-funcs],[use sh functions [[yes]]]),
	if test "$enableval" = "yes"; then
	    AC_DEFINE(HAS_BOURNE_FUNCS)
	fi, AC_DEFINE(HAS_BOURNE_FUNCS))dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([EVAL_ALIAS],[
EVAL_ALIAS:
This symbol, if defined, indicates that Modules should ask the shell
to evaluate aliases via stdout like other environment changes instead
of opening a file and then asking the shell to source the file and
then remove it.])dnl
AC_ARG_ENABLE(shell-eval,
	AC_HELP_STRING([--enable-shell-eval],[use shell alias eval [[no]]]),
	if test "$enableval" = "yes"; then
	    AC_DEFINE(EVAL_ALIAS)
	fi)dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([USE_FREE],[
USE_FREE:
By default, we will not call free() for any dynamic data. This
define enables these calls again.])dnl
AC_ARG_ENABLE(free,
	AC_HELP_STRING([--enable-free],[use free() calls [[no]]]),
	if test "$enableval" = "yes"; then
	    AC_DEFINE(USE_FREE)
	fi)dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([CACHE_AVAIL],[
CACHE_AVAIL:
This symbol, if defined, indicates that Modules should cache the
directories in MODULEPATH when doing an avail.])dnl
AC_ARG_ENABLE(cache,
	AC_HELP_STRING([--enable-cache],[use directory cache [[no]]]),
	if test "$enableval" = "yes"; then
	    AC_DEFINE(CACHE_AVAIL)
	fi)dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([CACHE_UMASK],[
CACHE_UMASK:
This symbol, if defined, is used as the umask for creating
cachefiles for the module avail command.])dnl
AC_ARG_WITH(cache-umask,
	AC_HELP_STRING([--with-cache-umask=<umask>],[use cache files umask=<umask> [[000]]]),
	if test "$withval" != "no"; then
	    test "$withval" = "yes" && withval=0
	    AC_DEFINE_UNQUOTED(CACHE_UMASK, ${withval})
	fi)
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([WITH_DEBUG_INFO],[WITH_DEBUG_INFO: Debug info level if defined])dnl
AC_ARG_WITH(debug,
	AC_HELP_STRING([--with-debug=<value>],[set debugging level <value> [[no]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="1"
		AC_DEFINE_UNQUOTED(WITH_DEBUG_INFO, ${withval})dnl
	fi)dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([VERSION_MAGIC],[
VERSION_MAGIC:
By default, .version files will require to have the modulefile
magic cookie to be valid, else an error message will be printed out.
Set to 0 to disable.])dnl
AC_ARG_ENABLE(version-magic,
	AC_HELP_STRING([--enable-version-magic],[set .version magic requirement  [[yes]]]),
	if test "$enableval" != "no"; then
		AC_DEFINE(VERSION_MAGIC,1)dnl
	else
		AC_DEFINE(VERSION_MAGIC,0)dnl
	fi, AC_DEFINE(VERSION_MAGIC,1))dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([BEGINENV],[
BEGINENV:
Modules can save the initial user environment in the .modulesbeginenv
when the first module is loaded.  If enabled then the module update
subcommand will work.])dnl
AC_ARG_ENABLE(beginenv,
	AC_HELP_STRING([--enable-beginenv],[use .modulesbeginenv for initial env [[no]]]),
	if test "$enableval" != "no"; then
		AC_DEFINE(BEGINENV,1)dnl
	fi)dnl
dnl ---------------------------------------------------------------------------
dnl  Logging
dnl  The following option allow switching logging on and off
dnl  and provide a possibility to set up the default facilities
dnl ---------------------------------------------------------------------------
dnl WITH_LOGGING	Whether syslogging is enabled
dnl
AC_SUBST(WITH_LOGGING)dnl
AH_TEMPLATE([WITH_LOGGING],[WITH_LOGGING: Syslog logging and facilities])dnl
AH_TEMPLATE([WITH_LOG_DIR],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_VERBOSE],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_INFO],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_DEBUG],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_TRACE],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_WARN],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_PROB],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_ERROR],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_FATAL],[])dnl
AH_TEMPLATE([WITH_LOG_FACILITY_PANIC],[])dnl
WITH_LOGGING=0
AC_ARG_ENABLE(logging,
	AC_HELP_STRING([--enable-logging],[turn logging on                 [[no]]]),
	if test "$enableval" = "yes"; then
		AC_DEFINE(WITH_LOGGING)
		WITH_LOGGING=1
	fi)
AC_ARG_WITH(log-dir,
	AC_HELP_STRING([--with-log-dir=<value>],[select an alternate log dir.  [[no]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="/tmp"
		AC_DEFINE_UNQUOTED(WITH_LOG_DIR, "$withval")
	fi)
AC_ARG_WITH(log-facility-verbose,
	AC_HELP_STRING([--with-log-facility-verbose[=<facility>]],[Log facility    [[stderr]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="stderr"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_VERBOSE, "$withval")
	fi)
AC_ARG_WITH(log-facility-info,
	AC_HELP_STRING([--with-log-facility-info[=<facility>]],[Log facility    [[local7.notice]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.notice"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_INFO, "$withval")
	fi)
AC_ARG_WITH(log-facility-debug,
	AC_HELP_STRING([--with-log-facility-debug[=<facility>]],[Log facility    [[local7.notice]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.notice"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_DEBUG, "$withval")
	fi)
AC_ARG_WITH(log-facility-trace,
	AC_HELP_STRING([--with-log-facility-trace[=<facility>]],[Log facility    [[local7.notice]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.notice"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_TRACE, "$withval")
	fi)
AC_ARG_WITH(log-facility-warn,
	AC_HELP_STRING([--with-log-facility-warn[=<facility>]],[Log facility    [[local7.warning]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.warning"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_WARN, "$withval")
	fi)
AC_ARG_WITH(log-facility-problem,
	AC_HELP_STRING([--with-log-facility-problem[=<facility>]],[Log facility    [[local7.error]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.error"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_PROB, "$withval")
	fi)
AC_ARG_WITH(log-facility-error,
	AC_HELP_STRING([--with-log-facility-error[=<facility>]],[Log facility    [[local7.error]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.error"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_ERROR, "$withval")
	fi)
AC_ARG_WITH(log-facility-fatal,
	AC_HELP_STRING([--with-log-facility-fatal[=<facility>]],[Log facility    [[local7.error]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.error"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_FATAL, "$withval")
	fi)
AC_ARG_WITH(log-facility-panic,
	AC_HELP_STRING([--with-log-facility-panic[=<facility>]],[Log facility    [[local7.error]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval="local7.error"
		AC_DEFINE_UNQUOTED(WITH_LOG_FACILITY_PANIC, "$withval")
	fi)
dnl ---------------------------------------------------------------------------
dnl  Tracing
dnl  The following option allow switching tracing on and off on a
dnl  per module command basis
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([WITH_TRACE_LOAD],[Tracking])dnl
AH_TEMPLATE([WITH_TRACE_UNLOAD],[])dnl
AH_TEMPLATE([WITH_TRACE_SWITCH],[])dnl
AH_TEMPLATE([WITH_TRACE_DISP],[])dnl
AH_TEMPLATE([WITH_TRACE_LIST],[])dnl
AH_TEMPLATE([WITH_TRACE_AVAIL],[])dnl
AH_TEMPLATE([WITH_TRACE_HELP],[])dnl
AH_TEMPLATE([WITH_TRACE_INIT],[])dnl
AH_TEMPLATE([WITH_TRACE_USE],[])dnl
AH_TEMPLATE([WITH_TRACE_UNUSE],[])dnl
AH_TEMPLATE([WITH_TRACE_UPDATE],[])dnl
AH_TEMPLATE([WITH_TRACE_PURGE],[])dnl
AH_TEMPLATE([WITH_TRACE_CLEAR],[])dnl
AH_TEMPLATE([WITH_TRACE_WHATIS],[])dnl
AH_TEMPLATE([WITH_TRACE_APROPOS],[])dnl

AC_ARG_WITH(trace-load,
	AC_HELP_STRING([--with-trace-load=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_LOAD, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_LOAD, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-unload,
	AC_HELP_STRING([--with-trace-unload=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_UNLOAD, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_UNLOAD, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-switch,
	AC_HELP_STRING([--with-trace-switch=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_SWITCH, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_SWITCH, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-disp,
	AC_HELP_STRING([--with-trace-disp=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_DISP, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_DISP, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-list,
	AC_HELP_STRING([--with-trace-list=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_LIST, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_LIST, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-avail,
	AC_HELP_STRING([--with-trace-avail=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_AVAIL, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_AVAIL, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-help,
	AC_HELP_STRING([--with-trace-help=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_HELP, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_HELP, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-init,
	AC_HELP_STRING([--with-trace-init=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_INIT, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_INIT, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-use,
	AC_HELP_STRING([--with-trace-use=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_USE, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_USE, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-unuse,
	AC_HELP_STRING([--with-trace-unuse=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_UNUSE, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_UNUSE, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-update,
	AC_HELP_STRING([--with-trace-update=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_UPDATE, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_UPDATE, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-purge,
	AC_HELP_STRING([--with-trace-purge=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_PURGE, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_PURGE, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-clear,
	AC_HELP_STRING([--with-trace-clear=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_CLEAR, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_CLEAR, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-whatis,
	AC_HELP_STRING([--with-trace-whatis=<value>],[specify a pattern for trace   [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_WHATIS, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_WHATIS, "$withval")
		fi
	fi)
AC_ARG_WITH(trace-apropos,
	AC_HELP_STRING([--with-trace-apropos=<value>],[specify a pattern for trace  [[no]]]),
	if test "$withval" != "no"; then
		if test "$withval" = "yes"; then
			AC_DEFINE_UNQUOTED(WITH_TRACE_APROPOS, _all_on)
		else
			AC_DEFINE_UNQUOTED(WITH_TRACE_APROPOS, "$withval")
		fi
	fi)
dnl ---------------------------------------------------------------------------
dnl Find libraries, functions and special programs
dnl	The dynamic loader library should only be probed for, if
dnl	static linking is not switched on!
dnl ---------------------------------------------------------------------------
AC_CHECK_LIB(m, log)
if test "${STATIC-foobar}" = "foobar"; then
    AC_CHECK_LIB(dl, dlopen)
fi

dnl
dnl--------------------------------------------------------------------
dnl Load the tclConfig.sh file
dnl--------------------------------------------------------------------

SC_PATH_TCLCONFIG
SC_LOAD_TCLCONFIG
SC_PUBLIC_TCL_HEADERS

dnl--------------------------------------------------------------------
dnl Load the tclxConfig.sh file
dnl search for tclx after tcl, not before to benefit from tcl_includes and
dnl--------------------------------------------------------------------

EM_PATH_TCLXCONFIG
EM_LOAD_TCLXCONFIG
if test x"$no_tclx" != x"true"; then
	AH_TEMPLATE([HAS_TCLXLIBS],[
HAS_TCLXLIBS:
This symbol, if defined, indicates that Modules should support
extended TCL (tclX) commands.])dnl
	AC_DEFINE(HAS_TCLXLIBS)dnl
fi

dnl ---------------------------------------------------------------------------
dnl
dnl   --with-module-path
dnl   --with-version-path
dnl   --with-tmp-dir
dnl   --with-autoload-path
dnl
dnl ---------------------------------------------------------------------------
dnl MODULEPATH		The modulepath we want
dnl
AC_SUBST(MODULEPATH)dnl
AH_TEMPLATE([MODULEPATH],[
MODULEPATH: Directory that contains all the module files])dnl
AC_ARG_WITH(module-path,
	AC_HELP_STRING([--with-module-path=<path>],[use module path=<path> [[EPREFIX/Modules/modulefiles]]]),
	MODULEPATH="$withval"
	AC_MSG_RESULT([MODULEPATH = $with_module_path])
	AC_DEFINE_UNQUOTED(MODULEPATH, "$withval")dnl
	,
dnl 	AC_MSG_WARN([if using '--with-module-path': need to specify a path!])
	withval="$BASEPREFIX/Modules/modulefiles"
	AC_MSG_WARN([will use MODULEPATH=$withval : rerun configure using --with-module-path to override default])
	MODULEPATH="$withval"
	AC_DEFINE_UNQUOTED(MODULEPATH, "$withval"))dnl
dnl ---------------------------------------------------------------------------
dnl VERSIONPATH		The modulepath different versions of modules
dnl
AC_SUBST(VERSIONPATH)dnl
AH_TEMPLATE([VERSIONPATH],[
VERSIONPATH: Directory that contains the env.modules version files.])dnl
AC_ARG_WITH(version-path,
	AC_HELP_STRING([--with-version-path=<path>],[use version path=<path> [[PREFIX/Modules/versions]]]),
	VERSIONPATH="$withval"
	AC_MSG_RESULT([VERSIONPATH = $with_version_path])
	AC_DEFINE_UNQUOTED(VERSIONPATH, "$withval")
	,
dnl AC_MSG_WARN([if using '--with-version-path': need to specify a path!])
	withval="$BASEPREFIX/Modules/versions"
	AC_MSG_WARN([will use VERSIONPATH=$withval : rerun configure using --with-version-path to override default])
	VERSIONPATH="$withval"
	AC_DEFINE_UNQUOTED(VERSIONPATH, "$withval"))dnl
dnl ---------------------------------------------------------------------------
AH_TEMPLATE([TMP_DIR],[TMP_DIR: tmp directory name])dnl
AC_ARG_WITH(tmp-dir,
	AC_HELP_STRING([--with-tmp-dir=<dir>],[use <dir> as a temporary dir [[/tmp]]]),
        if test ! -d $withval ; then
           AC_MSG_ERROR([tmp dir $withval does not exist])dnl
        fi
	AC_DEFINE_UNQUOTED(TMP_DIR, "$withval")dnl
	AC_MSG_RESULT([TMP_DIR = $withval]),
        if test -d "/tmp" ; then
           AC_DEFINE_UNQUOTED(TMP_DIR, "/tmp")dnl
        elif test -d "/var/tmp" ; then
           AC_DEFINE_UNQUOTED(TMP_DIR, "/var/tmp")dnl
        fi
	)dnl
dnl ---------------------------------------------------------------------------
dnl AUTOLOADPATH	directories where Tcl will search for libraries to load
dnl
AC_SUBST(AUTOLOADPATH)dnl
AH_TEMPLATE([AUTOLOADPATH],[
AUTOLOADPATH:
This symbol, if defined, is the "default" path of directories
Tcl will look for functions to autoload.  It is space separated!])dnl
AC_ARG_WITH(autoload-path,
	AC_HELP_STRING([--with-autoload-path=<path>],[use autoload path=<path> [[PREFIX/lib/tcl]]]),
	if test "$withval" = "no" || test "$withval" = "yes"; then
		if test "$prefix" != "NONE" ; then
			withval="$prefix/lib/tcl"
		else
			withval="$ac_default_prefix/lib/tcl"
		fi
	fi
	AUTOLOADPATH="$withval"
	AC_DEFINE_UNQUOTED(AUTOLOADPATH, "$withval"))dnl
dnl ---------------------------------------------------------------------------
dnl   --with-etc-path
dnl   --with-skel-path
dnl   --with-dot-ext
dnl ---------------------------------------------------------------------------
dnl ETCPATH		Where to find the /etc/"login" files
dnl
AC_SUBST(ETCPATH)dnl
AC_ARG_WITH(etc-path,
	AC_HELP_STRING([--with-etc-path=<path>],[use etc path=<path> [[/etc]]]),
	[if test "$withval" = "no" || test "$withval" = "yes"; then
		AC_MSG_WARN(
	[if using '--with-etc-path': need to specify a path! Using /etc])
		withval="/etc"
	fi
	ETCPATH="$withval"]
	,
	ETCPATH="/etc")dnl
dnl ---------------------------------------------------------------------------
dnl SKELPATH		Where to find the $HOME/dot file skeletons
dnl
AC_SUBST(SKELPATH)dnl
AC_ARG_WITH(skel-path,
	AC_HELP_STRING([--with-skel-path=<path>],[use skel path=<path> [[/etc/skel]]]),
	if test "$withval" = "no" || test "$withval" = "yes"; then
	AC_MSG_WARN([if using '--with-skel-path': need to specify a path! Using /etc/skel])
		withval="/etc/skel"
	fi
	SKELPATH="$withval"
	,
	SKELPATH="/etc/skel")
dnl ---------------------------------------------------------------------------
dnl DOT_EXT		Whether to use a dot-file extension
dnl EXTDIR		Which add.* script to include
dnl
AC_SUBST(DOT_EXT)dnl
AC_SUBST(EXTDIR)dnl
AH_TEMPLATE([DOT_EXT],[
DOT_EXT:
Set this to some extension value (e.g. .ext) to be appended onto
each shell dot-file name in the list for each shell for processing
the "module init*" commands.  For example - C-shell: .cshrc.ext,
.login.ext.])dnl
AC_ARG_WITH(dot-ext,
	AC_HELP_STRING([--with-dot-ext=<.ext>],[append <.ext> (default=.ext) to shell dot files [[no]]]),
	if test "$withval" != "no"; then
		test "$withval" = "yes" && withval=".ext"
		AC_DEFINE_UNQUOTED(DOT_EXT, "$withval")
		EXTDIR="ext"
	else
		AC_DEFINE(DOT_EXT, "")
		EXTDIR="etc"
	fi,
	AC_DEFINE(DOT_EXT, "")
	EXTDIR="etc"
)
dnl ---------------------------------------------------------------------------
dnl helpful configure --help comments
m4_divert_once([HELP_WITH],[[

The default value or disposition is listed in the square brackets above.
Beware of new defaults for use-cache, beginenv.

Depending on the above configuration options  the files are approximately
placed in the following directory structure :
PREFIX/Modules/($MODULES_VERSION/ if modules-versioning)
	bin/
	init/
	man/
		man1/
		man4/
	modulefiles/
PREFIX/Modules/
	modulefiles/
	(versions/ if modules-versioning)

System modulefiles can safely be placed in:
PREFIX/Modules/modulefiles
]])dnl
dnl ---------------------------------------------------------------------------
dnl Now put it all out!
dnl ---------------------------------------------------------------------------
AC_CONFIG_FILES([Makefile modules.lsm .spec doc/Makefile
	init/Makefile init/.modulespath
	doc/module.1 doc/modulefile.4
	etc/Makefile etc/add.modules etc/mkroot
	etc/skel/.cshrc etc/skel/.login 
	etc/skel/.kshenv etc/skel/.profile 
	etc/global/csh.cshrc etc/global/csh.login etc/global/csh.modules
	etc/global/bashrc etc/global/profile etc/global/profile.modules
	ext/Makefile ext/add.ext ext/mkroot
	ext/common/.cshrc ext/common/.login 
	ext/common/.kshenv ext/common/.profile 
	modulefiles/Makefile modulefiles/modules
	modulefiles/module-cvs modulefiles/module-info modulefiles/null
	modulefiles/use.own modulefiles/dot modulefiles/version
	])
dnl	init/bash init/csh init/ksh init/perl init/python init/sh init/zsh
AC_CONFIG_COMMANDS([default],[[date > stamp-h]],[[]])
AC_OUTPUT
dnl
